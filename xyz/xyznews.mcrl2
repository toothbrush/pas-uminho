map
%	n, m, n, r , q : Nat;
	NClients, NServers : Pos;

% =============================================================================

eqn
%	n = 115; % maximum time to deliver the response of a request
%	m = 100; % time taken to reply to r requests in media mode, or q requests in text mode
%	n =   5; % number of available servers
%	r =   5; % number of supported media requests so that the server replies in less than n miliseconds
%	q =  20; % number of supported test requests so that the server replies in less than n miliseconds

	NClients = 3;
	NServers = 2;

% =============================================================================

act
	clientRequest, serverRequest, serverResponse, clientResponse : Pos;
	sendRequest, receiveRequest, sendResponse, receiveResponse : Pos;
	process, wait, idle;
	sReq,rReq,sRes,rRes : Pos;

% =============================================================================

proc
	% XYZNews System
	System = comm({
			     sendRequest|clientRequest   -> sReq
			, clientResponse|receiveResponse -> rRes
			,  serverRequest|receiveRequest  -> rReq
			,   sendResponse|serverResponse  -> sRes},
		(Network || LoadBalancer(true,0) || ParallelServers(NServers)));

	% ---------------------------------------------------------------------------

	% Counts the number of connections/requests, defines the number of servers
	% online and their operating mode
	LoadBalancer(mm : Bool, nServers : Nat) =
		  ReceiveRequest(mm, nServers)
		+ SendResponse(mm, nServers);

	ReceiveRequest(mm : Bool, nServers : Nat) =
		sum cid : Pos . clientRequest(cid).serverRequest(cid).LoadBalancer(mm, nServers);

	SendResponse(mm : Bool, nServers : Nat) =
		sum cid : Pos . (serverResponse(cid).clientResponse(cid)).LoadBalancer(mm, nServers);

	% The network (receives connections/requests from clients and send them the
	% responses)
	Network = ParallelClients(NClients);

	% ---------------------------------------------------------------------------

	% Create a given number of servers in parallel
	ParallelServers(n:Pos) = (n>1) -> (Server(n) || ParallelServers(Int2Pos(n-1))) <> Server(1);
	
	% Receives requests (text or media mode) and provides the responses
	Server(id:Pos) = sum cid : Pos . (cid <= NClients) -> receiveRequest(cid).process.sendResponse(cid).Server(id);

	% Create a given number of clients in parallel
	ParallelClients(n:Pos) = (n>1) -> (Client(n) || ParallelClients(Int2Pos(n-1))) <> Client(1);

	% Sends requests and waits for responses
	Client(id:Pos) = sendRequest(id).wait.receiveResponse(id).idle.Client(id);
	
% =============================================================================

init System;

